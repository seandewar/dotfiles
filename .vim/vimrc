""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
"  Sean Dewar's Vim/Neovim Configuration <https://github.com/seandewar>
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

" determine a runtime dir to store user data.
" an alternative is using fnamemodify(expand('$MYVIMRC', ':p:h')), but if the
" .vimrc file is in the home directory, we'll end up cluttering it...
function! GetUserDir()
  if has('nvim')
    if has('win32')
      return expand('~/AppData/Local/nvim')
    else
      return expand('~/.config/nvim')
    endif
  else
    if has('win32')
      return expand('~/vimfiles')
    else
      return expand('~/.vim')
    endif
  endif
endfunction
let $VIMUSERDIR = GetUserDir()

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
"  Plugin Configurations (using Plug)
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

" run :PlugInstall to install new plugins
call plug#begin($VIMUSERDIR . '/plugged')

" utilities
Plug 'Shougo/echodoc.vim' " display completion signatures in command line
Plug 'SirVer/ultisnips' " snippets engine
Plug 'derekwyatt/vim-fswitch' " switch between companion files (.h, .c/.cc etc.)
Plug 'easymotion/vim-easymotion' " easier motions using <leader><leader>
Plug 'lifepillar/vim-mucomplete'
Plug 'ludovicchabant/vim-gutentags' | Plug 'skywind3000/gutentags_plus'
Plug 'mhinz/vim-startify' " start screen
Plug 'sheerun/vim-polyglot' " language support package
Plug 'tpope/vim-commentary'
Plug 'tpope/vim-dispatch' " async jobs using :Dispatch
Plug 'tpope/vim-fugitive' " git integration
Plug 'tpope/vim-surround'
Plug 'tpope/vim-vinegar' " enhancements for the netrw directory viewer
Plug 'vim-utils/vim-troll-stopper' " highlight troll unicode (e.g. the greek ;)
Plug 'w0rp/ale' " vim8/nvim async linting engine + lsp support

" appearance
Plug 'itchyny/lightline.vim' | Plug 'maximbaz/lightline-ale'
Plug 'morhetz/gruvbox' " colorscheme

call plug#end()

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
"  Vim Behaviour
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

" don't crowd working directories with swap files.
" we'll use the same path regardless of nvim/vim so both programs are able to
" detect the same swap files
if !isdirectory(expand('~/.vim/swap'))
  call mkdir(expand('~/.vim/swap'), 'p')
endif
set directory^=~/.vim/swap//

" set character encoding
set encoding=utf8 fileencoding=utf8 termencoding=utf8

" disable unsafe commands in local .vimrc files
set secure

" don't unload modified buffers that are not displayed within a window
set hidden

" open splits on the bottom/right instead of top/left
set splitbelow splitright

" always show at least 1 line above or below the cursor
set scrolloff=1

" enable mouse for all modes, don't hide cursor when typing, right-click
" displays context menu
set mouse=a nomousehide mousemodel=popup

" search options
set hlsearch incsearch ignorecase smartcase

" completion matches for commands (after pressing <tab> or ^D for a list &
" <tab><tab> for the wildmenu)
set wildmenu wildmode=list:longest,full wildignorecase

" always display the status and tab lines
set laststatus=2 showtabline=2

" don't show mode in the command-line (we already see it in the buffer line)
set noshowmode

" disable bell noises
set belloff=all

" display open file name in terminal title bar
set title

" set tab sizes
set tabstop=2 softtabstop=2 shiftwidth=2 autoindent expandtab

" automatically wrap text inserted at 80 characters
set textwidth=79

" backspace settings
set backspace=indent,eol,start

" ensure file type, file plugin and file indent detection is on
filetype plugin indent on

" configure completion menu
set completeopt=menuone,preview,noinsert

" close completion preview after finishing completion
augroup AutoCloseCompletionPreview
autocmd!
autocmd CompleteDone * pclose
augroup END

" wait longer for key timeouts
set timeoutlen=1500

" startify bookmarks
let g:startify_bookmarks = [ { 'V': $MYVIMRC } ]

" start echodoc automatically
let g:echodoc#enable_at_startup = 1

" configure ultisnips - I like to store my snippets globally
let g:UltiSnipsEditSplit = 'vertical'
let g:UltiSnipsSnippetsDir = $VIMUSERDIR . '/MyUltiSnips'
let g:UltiSnipsSnippetDirectories = [ g:UltiSnipsSnippetsDir ]

" configure vim-gutentags and gutentags_plus
let g:gutentags_modules = [ 'ctags', 'gtags_cscope' ]
let g:gutentags_project_root = [ '.root' ] " extra project root markers
let g:gutentags_cache_dir = expand('~/.cache/tags') " save tags in user root dir
let g:gutentags_define_advanced_commands = 1
let g:gutentags_plus_switch = 1 " auto-focus gutentags quickfix buffer
let g:gutentags_plus_nomap = 1 " we'll explicitly define keymaps

" ale fixing and linting preferences
let g:ale_fixers = { '*': [ 'remove_trailing_lines', 'trim_whitespace' ] }
let g:ale_linters = { 'cpp': [ ], 'c': [ ] } " my preference changes daily
let g:ale_fix_on_save = 1

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
"  Vim Appearance
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

" syntax highlighting
syntax on

" show tab characters (useful for snippets)
set list listchars=tab:..

" only show the tabline if at least two tabs are open
set showtabline=1

" show the ruler and make sure we show hybrid relative line numbers on the side
" of all buffer windows (including windows to non-file buffers)
set ruler
augroup AutoWindowNumber
  autocmd!
  autocmd BufWinEnter * setlocal number relativenumber
augroup END

" make sure we see numbers in netrw
let g:netrw_bufsettings =
      \ 'nomodifiable nomodified number relativenumber nobuflisted readonly'

" highlight the line that the cursor is on for active windows only
augroup ActiveWindowCursorLine
  autocmd!
  autocmd VimEnter,WinEnter * setlocal cursorline
  autocmd WinLeave * setlocal nocursorline
augroup END

" configure 80 and 120 column indicator only in insert mode
augroup ColorColumnInsertMode
  autocmd!
  autocmd InsertEnter * setlocal colorcolumn=81,121
  autocmd InsertLeave * setlocal colorcolumn=
augroup END

" configure color scheme
set background=dark
let g:gruvbox_contrast_dark = 'hard'
colorscheme gruvbox

" ale gutter error/warning symbols and message config
let g:ale_sign_column_always = 1
let g:ale_sign_error = 'E>'
let g:ale_sign_warning = 'W>'
let g:ale_echo_msg_error_str = 'error'
let g:ale_echo_msg_warning_str = 'warning'
let g:ale_echo_msg_info_str = 'info'
let g:ale_echo_msg_format = '[%severity%] %s [%linter%]'

" lightline-ale symbols
let g:lightline#ale#indicator_errors = 'E:'
let g:lightline#ale#indicator_warnings = 'W:'
let g:lightline#ale#indicator_checking = 'Linting...'
let g:lightline#ale#indicator_ok = 'Lint OK'

" configure lightline
let g:lightline = {
      \ 'colorscheme': 'gruvbox',
      \ 'component_expand': {
      \   'linter_checking': 'lightline#ale#checking',
      \   'linter_errors': 'lightline#ale#errors',
      \   'linter_warnings': 'lightline#ale#warnings',
      \   'linter_ok': 'lightline#ale#ok'
      \ },
      \ 'component_type': {
      \   'linter_checking': 'left',
      \   'linter_errors': 'error',
      \   'linter_warnings': 'warning',
      \   'linter_ok': 'left'
      \ },
      \ 'component_function': { 'gitbranch': 'fugitive#head' },
      \ 'active': {
      \   'left': [ [ 'mode', 'paste' ],
      \             [ 'gitbranch', 'readonly', 'filename', 'modified' ] ],
      \   'right': [ [ 'linter_checking', 'linter_warnings',
      \                'linter_errors', 'linter_ok' ],
      \              [ 'lineinfo' ],
      \              [ 'percent' ],
      \              [ 'fileformat', 'fileencoding', 'filetype' ] ]
      \ },
      \ 'inactive': {
      \   'left': [ [ 'readonly', 'filename', 'modified' ] ],
      \   'right': [ [ 'lineinfo' ],
      \              [ 'percent' ],
      \              [ 'fileformat', 'fileencoding', 'filetype' ] ]
      \ },
      \ 'tabline': { 'left': [ [ 'tabs' ] ], 'right': [ [ '' ] ] }
      \ }

" function to refresh lightline
function! g:LightlineRefresh()
  if !exists('g:loaded_lightline')
    return
  endif
  call lightline#init()
  call lightline#colorscheme()
  call lightline#update()
endfunction

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
"  Vim Keymappings
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

" general rebinds
" make sure c-s flow ctrl is disabled for the terminal - press c-q to unfreeze
nnoremap <c-s> :write<cr>
inoremap <c-s> <c-\><c-o>:write<cr>
nnoremap <leader>/ :set nohlsearch<cr>
nnoremap <leader><esc> :Startify<cr>

" allow toggling between rnu and nu mode
function! g:ToggleNumber()
  if &relativenumber
    setlocal norelativenumber
  else
    setlocal relativenumber
  endif
endfunction
nnoremap <silent> <leader>L :call g:ToggleNumber()<cr>

" configure utilsnips keybinds
let g:UltiSnipsExpandTrigger = '<c-j>'
let g:UltiSnipsListSnippets = '<c-space>'
let g:UltiSnipsJumpForwardTrigger = '<c-j>'
let g:UltiSnipsJumpBackwardTrigger = '<c-n>'

" netrw (dir explorer) keybinds
nnoremap <leader>dd :Explore<cr>
nnoremap <leader>de :Explore<space>
nnoremap <leader>ds :Sexplore<space>
nnoremap <leader>dv :Vexplore<space>

" buffer keybinds
nnoremap <leader>bb :buffers<cr>:buffer<space>
nnoremap <leader>bs :buffers<cr>:sbuffer<space>
nnoremap <leader>bv :buffers<cr>:vertical sbuffer<space>
nnoremap <leader>bd :buffers<cr>:bdelete<space>
nnoremap <leader>bt :buffers<cr>:tab sbuffer<space>
nnoremap <leader>bn :bnext<cr>
nnoremap <leader>bN :bprevious<cr>

" loclist window keybinds
nnoremap <leader>ll :lopen<cr>
nnoremap <leader>ln :lnext<cr>
nnoremap <leader>lN :lprevious<cr>

" quickfix window keybinds
nnoremap <leader>qq :copen<cr>
nnoremap <leader>qn :cnext<cr>
nnoremap <leader>qN :cprevious<cr>

" configure startify session keybinds
nnoremap <leader>ss :SSave<cr>
nnoremap <leader>sl :SLoad<cr>
nnoremap <leader>sd :SDelete<cr>
nnoremap <leader>sc :SClose<cr>

" configure vim-fswitch keybinds
nnoremap <leader>oo :FSHere<cr>
nnoremap <leader>oh :FSLeft<cr>
nnoremap <leader>ol :FSRight<cr>
nnoremap <leader>ok :FSAbove<cr>
nnoremap <leader>oj :FSBelow<cr>
nnoremap <leader>oH :FSSplitLeft<cr>
nnoremap <leader>oL :FSSplitRight<cr>
nnoremap <leader>oK :FSSplitAbove<cr>
nnoremap <leader>oJ :FSSplitBelow<cr>

" configure gutentags_plus keybinds
nnoremap <leader>ts :GscopeFind s <c-r><c-w><cr>
nnoremap <leader>tg :GscopeFind g <c-r><c-w><cr>
nnoremap <leader>tc :GscopeFind c <c-r><c-w><cr>
nnoremap <leader>tt :GscopeFind t <c-r><c-w><cr>
nnoremap <leader>te :GscopeFind e <c-r><c-w><cr>
nnoremap <leader>tf :GscopeFind f <c-r>=expand("<cfile>")<cr><cr>
nnoremap <leader>ti :GscopeFind i <c-r>=expand("<cfile>")<cr><cr>
nnoremap <leader>td :GscopeFind d <c-r><c-w><cr>
nnoremap <leader>ta :GscopeFind a <c-r><c-w><cr>

" configure vim-fugitive keybinds
nnoremap <leader>gg :Gstatus<cr>
nnoremap <leader>gs :Gstatus<cr>
nnoremap <silent> <leader>gl :Glog<cr>:copen<cr>
nnoremap <silent> <leader>gL :0Glog<cr>:copen<cr>
nnoremap <leader>ge :Gedit<cr>
nnoremap <leader>gd :Gdiff<cr>
nnoremap <leader>gb :Gblame<cr>
nnoremap <leader>gc :Gcommit<cr>
nnoremap <leader>gC :Gcommit -a<cr>
nnoremap <leader>gw :Gwrite<cr>
nnoremap <leader>gr :Gread<cr>
nnoremap <leader>gps :Gpush<cr>
nnoremap <leader>gpl :Gpull<cr>

" configure ale keybinds (most of these binds only work for lsp servers)
imap <c-tab> <plug>(ale_complete)
nnoremap <leader>al :ALELint<cr>
nnoremap <leader>af :ALEFix<cr>
nnoremap <leader>ah :ALEHover<cr>
nnoremap <leader>ai :ALEHover<cr>
nnoremap <leader>as :ALESymbolSearch<space>
nnoremap <leader>ar :ALEFindReferences<cr>
nnoremap <leader>ad :ALEGoToDefinition<cr>
nnoremap <leader>aD :ALEGoToDefinitionInSplit<cr>
nnoremap <leader>at :ALEGoToTypeDefinition<cr>
nnoremap <leader>aT :ALEGoToTypeDefinitionInSplit<cr>
