" Add Neovim's config directory after the first entry in the 'runtimepath'.
" Add the after/ directory before the last.
"
" NOTE: Don't bother sharing Nvim's data directories; it usually uses a
"       different format for things.
let g:neovim_stow_package_dir =
            \ expand('<sfile>')->resolve()->fnamemodify(':p:h:h:h')
            \ .. '/neovim'

let s:config_dir = g:neovim_stow_package_dir .. '/dot-config/nvim'
let s:rtp = split(&runtimepath, ',')

call insert(s:rtp, s:config_dir, 1)
call insert(s:rtp, s:config_dir .. '/after', -1)

let &runtimepath = join(s:rtp, ',')
let &packpath = &runtimepath

unlet s:rtp

" Run init.vim if it's readable; it's designed to be compatible with Vim.
let s:nvimrc = s:config_dir .. '/init.vim'
if filereadable(s:nvimrc)
    execute 'source ' .. s:nvimrc
endif

unlet s:nvimrc s:config_dir
